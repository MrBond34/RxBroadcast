plugins {
     id 'com.jfrog.bintray' version '1.6'
}
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'

apply from: 'gradle/deploy.gradle'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
}

dependencies {
    compile 'com.esotericsoftware:kryo:3.0.3'
    compile 'io.reactivex:rxjava:1.1.5'
    testCompile 'junit:junit:4.12'
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << '-Xlint:unchecked' << '-Xdiags:verbose'
        }
    }
}

checkstyle {
    toolVersion = '6.15'
}

jacoco {
    toolVersion = '0.7.9'
}

javadoc {
    options.optionFiles << file('config/javadoc.opts')
}

test {
    exclude 'rx/broadcast/integration/**'
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
        events 'passed', 'skipped', 'failed'
    }
}

task testJar(type: Jar) {
    from sourceSets.test.output + sourceSets.main.output
    from(configurations.testCompile.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'META-INF/**'
    }

    classifier 'tests'
    description 'Assembles a jar archive containing the test classes.'
    group 'verification'
}
